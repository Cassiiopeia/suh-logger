plugins {
    // Spring Boot 테스트 + BOM 관리
    id 'org.springframework.boot' version '3.4.4'
    id 'io.spring.dependency-management' version '1.1.7'
    // java-library > 순수 JAR
    id 'java-library'
    id 'maven-publish'
}

group = 'me.suhsaechan'
version = project.findProperty('version') ?: '0.0.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

// Spring Boot BOM
dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:3.4.4"
    }
}

// BOM에 없는 Lombok만 별도 버전
ext {
    lombokVersion = '1.18.26'
}

dependencies {
    // ============== 런타임/컴파일 ==============

    // Spring Boot Starter (core + logging)
    implementation 'org.springframework.boot:spring-boot-starter'
    // AOP 지원
    implementation 'org.springframework.boot:spring-boot-starter-aop'

    // Jackson
    api 'com.fasterxml.jackson.core:jackson-databind'
    api 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    // SLF4J API (Lombok @Slf4j가 생성하는 인터페이스)
    api 'org.slf4j:slf4j-api'

    // Lombok
    compileOnly     "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    // ============== 테스트 ==============
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.slf4j:slf4j-simple'
}

tasks.named('test') {
    useJUnitPlatform()
}

// Spring Boot JAR 비활성화
bootJar {
    enabled = false
}

// 일반 JAR 활성화
jar {
    enabled = true
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name        = 'suh-logger'
                description = '서새찬 로깅 라이브러리'
                url         = 'https://github.com/Cassiiopeia/suh-logger/'
            }
        }
    }
    repositories {
        mavenLocal()
        maven {
            name = 'nexus'
            url  = uri(
                    version.endsWith('SNAPSHOT')
                            ? "http://suh-project.synology.me:9999/repository/maven-snapshots/"
                            : "http://suh-project.synology.me:9999/repository/maven-releases/"
            )
            allowInsecureProtocol = true
            credentials {
                username = project.findProperty('nexusUsername')
                password = project.findProperty('nexusPassword')
            }
        }
    }
}
